/*=============================================================================
  Masaki Chikashi Ghost Templates
  その他イベント辞書
  Last up date: 2018/03/15
=============================================================================*/

/*-----------------------------------------------------------------------------
  ホットキー設定
-----------------------------------------------------------------------------*/
OnKeyPress
{
	case reference[0] {
		when 'return' {
			OnAITalk
		}
		when 'back' {
			OnLastTalk
		}
		when 'escape' {
			Cancel
		}
		when 'tab' {
			Setting
		}
		when 'g' {
			'\![reload,ghost]\e'
		}
		when 'r' {
			'\![reload,shiori]\e'
		}
		when 's' {
			'\![reload,shell]\e'
		}
	}
}

/*-----------------------------------------------------------------------------
  インストール開始
-----------------------------------------------------------------------------*/
OnInstallBegin
{
	"\1\s[10]\0\s[0]インストール開始。\n\n[half]\e"
}

/*-----------------------------------------------------------------------------
  インストール完了
-----------------------------------------------------------------------------*/
OnInstallComplete
{
	"\1\s[10]\0\s[0]インストール完了。\n\n[half]（"
	--
	case reference[0] {
		when 'shell'           ; 'シェル'
		when 'ghost'           ; 'ゴースト'
		when 'balloon'         ; 'バルーン'
		when 'headline'        ; 'ヘッドライン'
		when 'plugin'          ; 'プラグイン'
		when 'supplement'      ; 'サプリメント'
		when 'calendar skin'   ; 'カレンダースキン'
		when 'calendar plugin' ; 'カレンダープラグイン'
		when 'rss'             ; 'RSS'
		others                 ; (EscapeAllTags(reference[0]))
	}
	--
	//アーカイブ名
	"『%(EscapeAllTags(reference[1]))』）"
	--
	"\e"
}

/*-----------------------------------------------------------------------------
  インストール失敗
-----------------------------------------------------------------------------*/
OnInstallFailure
{
	"\1\s[10]\0\s[0]インストール失敗。\n\n[half]（失敗理由："
	--
	case reference[0] {
		when 'unlha32'			; "unlha32ロード失敗"
		when 'extraction'		; "アーカイブ破損"
		when 'invalid type'		; "install.txtが不正"
		when 'artificial '		; 'ユーザによる中断'
		when 'unsupported'		; 'サポート外アーカイブ'
		others				; (EscapeAllTags(reference[0]))
	}
	--
	"）\e"
}

/*-----------------------------------------------------------------------------
  インストール拒否
-----------------------------------------------------------------------------*/
OnInstallRefuse
{
	"\1\s[10]\0\s[0]これはゴースト『%(EscapeAllTags(reference[0]))』用のもの。\e"
}

/*-----------------------------------------------------------------------------
  ネットワーク更新開始
-----------------------------------------------------------------------------*/
OnUpdateBegin
{
	"\1\s[10]\0\s[0]何か届いた？\e"
}

/*-----------------------------------------------------------------------------
  ネットワーク更新準備完了
-----------------------------------------------------------------------------*/
OnUpdateReady
{
	//「On_useorigin1」の設定により0から数えるため+1
	"\1\s[10]\0\s[0]%((TOINT(reference[0]))+1)個、荷物が来てるね。\e"
}

/*-----------------------------------------------------------------------------
  ネットワーク更新終了
-----------------------------------------------------------------------------*/
OnUpdateComplete
{
	if reference[0] == "none"
	{
		//更新すべきファイルが無かった
		"\1\s[10]\0\s[0]気のせいか。\e"
	}
	else
	{
		//実際に更新が完了した
		"\1\s[10]\0\s[1]ナニが届いたのさ。新しいオモチャ？\e"
	}
}

/*-----------------------------------------------------------------------------
  ネットワーク更新失敗
-----------------------------------------------------------------------------*/
OnUpdateFailure
{
	"\1\s[10]\0\s[0]ネットワーク更新失敗。\n\n[half]/
	（失敗理由："
	--
	case TOSTR(reference[0]) {
		when 'timeout' , "too slow"	; "タイムアウト"
		when 'md5 miss'			; "MD5値不一致"
		when '404'			; "該当ファイルなし"
		when 'artificial '		; "手動中断"
		when 'fileio'			; "ディスク容量不足"
		when 'readonly'			; "ディレクトリ書き込み禁止"
		others				; (EscapeAllTags(reference[0]))
	}
	--
	"）\e"
}

/*-----------------------------------------------------------------------------
  MD5値照合開始
-----------------------------------------------------------------------------*/
/* OnUpdate.OnMD5CompareBegin
{
	'\1\s[10]\0\s[0]（MD5値照合開始）\e'
} */

/*-----------------------------------------------------------------------------
  MD5値一致
-----------------------------------------------------------------------------*/
/* OnUpdate.OnMD5CompareComplete
{
	'\1\s[10]\0\s[0]（MD5値一致）\e'
}
*/

/*-----------------------------------------------------------------------------
  MD5値不一致
-----------------------------------------------------------------------------*/
OnUpdate.OnMD5CompareFailure
{
	"\1\s[10]\0\s[0]（MD5値不一致）\e"
}

/*-----------------------------------------------------------------------------
  シェル変更開始　（省略可）
-----------------------------------------------------------------------------*/
/* OnShellChanging
{
	"\1\s[10]\0\s[0]シェルを『%(EscapeAllTags(reference[0]))』に変えるよ。\e"
} */

/*-----------------------------------------------------------------------------
  シェル変更終了　（省略可）
-----------------------------------------------------------------------------*/
/* OnShellChanged {
	"\1\s[10]\0\s[0]シェルを『%(EscapeAllTags(reference[0]))』に変えたよ。\e"
} */

/*-----------------------------------------------------------------------------
  最小化　（省略可）
-----------------------------------------------------------------------------*/
OnWindowStateRestore
{
	"\1\s[10]\0\s[0]床はやっぱり、冷たいね。\nでも、そこがイイ。\e"
}

/*-----------------------------------------------------------------------------
  見切れ発生
-----------------------------------------------------------------------------*/
mikireStartTalk
{
	"\1\s[10]\0\s[3]ん、ぅ。\e"
}

/*-----------------------------------------------------------------------------
  見切れ終了
-----------------------------------------------------------------------------*/
mikireEndTalk
{
	"\1\s[10]\0\s[0]……ふぅ。\e"
}

/*-----------------------------------------------------------------------------
  重なり発生
-----------------------------------------------------------------------------*/
kasanariStartTalk
{
	"\1\s[10]\0\s[3]ん、ぅ。\e"
}

/*-----------------------------------------------------------------------------
  重なり終了
-----------------------------------------------------------------------------*/
kasanariEndTalk
{
	"\1\s[10]\0\s[0]……ふぅ。\e"
}

/*-----------------------------------------------------------------------------
  updates2.dau作成開始
-----------------------------------------------------------------------------*/
OnUpdatedataCreating
{
	"\1\s[10]\0\s[0]（アップデートデータファイル作成開始）\e"
}

/*-----------------------------------------------------------------------------
  updates2.dau作成完了
-----------------------------------------------------------------------------*/
OnUpdatedataCreated
{
	"\1\s[10]\0\s[0]（アップデートデータファイル作成完了）\e"
}

/*-----------------------------------------------------------------------------
  NARファイル作成開始
-----------------------------------------------------------------------------*/
OnNarCreating
{
	"\1\s[10]\0\s[0]（NARファイル作成開始）\n\n[half]/
	//アーカイブ名
	『%(EscapeAllTags(reference[0]))』\n/
	//アーカイブパス
	\_qPath > %(EscapeAllTags(reference[1]))\_q\e"
}

/*-----------------------------------------------------------------------------
  NARファイル作成完了
-----------------------------------------------------------------------------*/
OnNarCreated
{
    "\1\s[10]\0\s[0]（NARファイル作成完了）\n\n[half]"
	--
	case reference[2] {
		when 'shell'			; 'シェル'
		when 'ghost'			; 'ゴースト'
		when 'balloon'			; 'バルーン'
		when 'headline'			; 'ヘッドライン'
		when 'plugin'			; 'プラグイン'
		when 'supplement'		; 'サプリメント'
		when 'calendar skin'		; 'カレンダースキン'
		when 'calendar plugin'		; 'カレンダープラグイン'
		when 'rss'			; 'RSS'
		others				; (EscapeAllTags(reference[2]))
	}
	--
	//アーカイブ名
	"『%(EscapeAllTags(reference[0]))』\n/
	//アーカイブパス
	\_qPath > %(EscapeAllTags(reference[1]))\_q\e"
}

/*-----------------------------------------------------------------------------
  時計合わせ開始
-----------------------------------------------------------------------------*/
OnSNTPBegin
{
	"\1\s[10]\0\s[0]%(EscapeAllTags(reference[0]))から、正確な時刻を取得するね。\e"
}

/*-----------------------------------------------------------------------------
  時刻取得成功、比較開始
-----------------------------------------------------------------------------*/
OnSNTPCompare
{
	_current = reference[1]
	_local = reference[2]

	"\1\s[10]\0\s[0]/
	正しい時刻は %(_current[0])/%(_current[1])/%(_current[2]) %(_current[3]):%(_current[4]):%(_current[5])\w2\n/
	こちらの時刻は %(_local[0])/%(_local[1])/%(_local[2]) %(_local[3]):%(_local[4]):%(_local[5])\w2\n/
	\n"
	--
	if reference[3] == 0
	{
		"ずれてないよ。\e"
	}
	else
	{
		"%(reference[3])秒、ずれてるね。\n[200%]/
		■\q[時刻を合わせる,OnSNTPEXECUTE]/
		■\q[合わせない,OnSNTPCANCEL]\e"
	}
}

/*-----------------------------------------------------------------------------
  時計合わせ実行
-----------------------------------------------------------------------------*/
OnSNTPEXECUTE
{
	"\1\s[10]\0\s[0]\6時刻、合わせたよ。\e"
}

/*-----------------------------------------------------------------------------
  時計合わせ中止
-----------------------------------------------------------------------------*/
OnSNTPCANCEL
{
	"\1\s[10]\0\s[0]じゃ、そのまま。\e"
}

/*-----------------------------------------------------------------------------
  時計合わせ失敗
-----------------------------------------------------------------------------*/
OnSNTPFailure
{
	"\1\s[10]\0\s[0]うまくいかなかったかな？\e"
}

/*-----------------------------------------------------------------------------
  時計合わせ完了
-----------------------------------------------------------------------------*/
OnSNTPCorrect
{
	"\1\s[10]\0\s[0]修正、終わったさ。\e"
}

/*-----------------------------------------------------------------------------
  メールチェック開始
-----------------------------------------------------------------------------*/
OnBIFFBegin
{
	"\1\s[10]\0\s[0]新着メール、チェックするよ。\e"
}

/*-----------------------------------------------------------------------------
  メールチェック成功
-----------------------------------------------------------------------------*/
OnBIFFComplete
{
	if reference[0] == 0
	{
		//合計メール数0
		"\1\s[10]\0\s[0]あたらしいメールはないね。\e"
	}
	if reference[3] > 0
	{
		//新着メールあり
		"\1\s[10]\0\s[0]あたらしいメールが、%(reference[3])通あったよ。\n/
		（メール数合計%(reference[0])通／%(reference[1])byte）\n[200%]/
		■\q[メーラーを開く,OpenMailer]　/
		■\q[Cancel,CancelOpenMailer]\e"
	}
	else
	{
		//新着メールなし
		"\1\s[10]\0\s[0]あたらしいメールはないね。\n/
		（メール数合計%(reference[0])通／%(reference[1])byte）\n[200%]/
		■\q[メーラーを開く,OpenMailer]　/
		■\q[Cancel,CancelOpenMailer]\e"
	}

}

//メーラーを開く
OpenMailer
{
	"\C\![open,mailer]\e"
}

//メーラーを開かない
CancelOpenMailer
{
	"\1\s[10]\b[-1]\0\s[0]\b[-1]\e"
}

/*-----------------------------------------------------------------------------
  メールチェック失敗
-----------------------------------------------------------------------------*/
OnBIFFFailure
{
	"\1\s[10]\0\s[0]んー、なんだかうまくいかなかったよ。/
	\n\n[half]（失敗理由："
	--
	case reference[0] {
		when 'timeout' ; '応答なし'
		when 'kick'    ; '認証失敗'
		when 'defect'  ; 'メール設定の不備'
		others         ; (EscapeAllTags(reference[0]))
	}
	--
	"）\e"
}

/*-----------------------------------------------------------------------------
  ヘッドラインセンス開始
-----------------------------------------------------------------------------*/
OnHeadlinesenseBegin
{
	"\1\s[10]\0\s[0]『%(EscapeAllTags(reference[0]))』のヘッドラインを取得するよ。\e"
}

/*-----------------------------------------------------------------------------
  ヘッドラインセンス読み上げ
-----------------------------------------------------------------------------*/
OnHeadlinesense.OnFind
{
	//\![set,autoscroll,disable] 自動スクロール防止タグ
	//\![set,choicetimeout,-1] タイムアウトバルーン閉じ防止タグ
	"\1\s[10]\0\s[0]\b[2]\![set,autoscroll,disable]\![set,choicetimeout,-1]\_q"
	--
	//はじめのセリフ
	if reference[2] == "First" || reference[2] == "First and Last"
	{
		//最初のページ
		"『%(EscapeAllTags(reference[0]))』"
	}
	elseif reference[2] == "Next"
	{
		//真ん中あたりのページ
		"『%(EscapeAllTags(reference[0]))』"
	}
	elseif reference[2] == "Last"
	{
		//最後のページ
		"『%(EscapeAllTags(reference[0]))』 (Last)"
	}
	--
	//ヘッドライン本体
	"\_q\n\n[half]%(reference[3])\n"
	--
	//選択肢
	if reference[2] == "First" || reference[2] == "Next"
	{
		//次のページがある場合
		"■\q[Next⇒,]　/
		■\q[Website,%(reference[1])]　/
		■\q[Cancel,HeadlineCancel]\e"
	}
	else
	{
		//次のページがない場合
		"■\q[Website,%(reference[1])]　/
		■\q[Cancel,HeadlineCancel]\e"
	}
}

/*-----------------------------------------------------------------------------
  ヘッドライン終了
-----------------------------------------------------------------------------*/
HeadlineCancel
{
	"\1\s[10]\b[-1]\0\s[0]\b[-1]\e"
}

/*-----------------------------------------------------------------------------
  ヘッドライン更新がなかった
-----------------------------------------------------------------------------*/
OnHeadlinesenseComplete
{
	"\1\s[10]\0\s[0]更新されてないね。\e"
}

/*-----------------------------------------------------------------------------
  ヘッドラインセンス失敗
-----------------------------------------------------------------------------*/
OnHeadlinesenseFailure
{
	"\1\s[10]\0\s[0]んー、なんだかうまくいかなかったよ。\n\n[half]/
	（失敗理由："
	--
	//ヘッドラインの「解析不能」は主にページデザイン変更が原因
	--
	if	'download' _in_ reference[0]	; 'ファイル取得不可'
	elseif	'analyze' _in_ reference[0]	; '解析不能'
	else					; (EscapeAllTags(reference[0]))
	--
	"）\e"
}
