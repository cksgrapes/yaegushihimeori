/*=============================================================================
  Masaki Chikashi Ghost Templates
  コミュニケート設定辞書
  Last up date: 2018/03/15
=============================================================================*/

/*-----------------------------------------------------------------------------
  選択コミュニケートコア
-----------------------------------------------------------------------------*/
Communicate
{
	"\1\s[10]\0\s[0]"
	--
	"ん、どしたの。"
	"なにかする？"
	--
	GetWordList
}

CommunicateEx
{
	GetWordList
}

GetWordList
{
	"\1\s[10]\_w[300]"
	--
	_text = ""
	for _i=0 ; _i < 4 ; _i++
	{
		_text += CommWordList + "\n"
	}
	_text
	--
	"\n◆\q[別の台詞,CommunicateEx]\n/
    ◆\q[言いたい台詞がない,OnInputCommWord]\n/
    ◆\q[好きな言葉で話しかける,OnFreeComm]\n/
	◆\q[キャンセル,Cancel]\![set,choicetimeout,30000]\e"
}

/*-----------------------------------------------------------------------------
  選択コミュニケート台詞を送信
-----------------------------------------------------------------------------*/
OnInputCommWord
{
  _content = "姫織に言いたい台詞をご記入ください"
  _id      = 'OnConfirmCommWord'
	InputFeedback(_content,_id)
}

OnConfirmCommWord
{
  _ref     = reference[0]
  _content = "以下の台詞でよろしいですか？"
  _id      = 'OnSendCommWord'
  ConfirmFeedback(_ref,_content,_id)
}

OnSendCommWord
{
  _content = reference[0]
  _category = '姫織に言いたい台詞'
  _id = 'OnExecuteSendCommWord'
  SendFeedback(_content,_category,_id)
}

OnExecuteSendCommWord
{
  "\1\s[10]\_q（台詞を送信しました。次回の更新をお待ちください）\_q\e"
}

OnExecuteSendCommWordFailure
{
  SendFeedbackFailure
}

/*-----------------------------------------------------------------------------
  フリーコミュニケートコア
-----------------------------------------------------------------------------*/
OnFreeComm
{
  "\![open,communicatebox]\e"
}

OnCommunicateInputCancel
{
  Communicate
}

ReplyToUser
{
  _content = EscapeDangerousTags(reference[1])
  FreeCommunicate(_content)
}

/*-----------------------------------------------------------------------------
  未対応コミュワード処理
-----------------------------------------------------------------------------*/
UserSaidIgnorant
{
  if (sendsaidword == 1) {
    _content = _argv[0]
    _category = '未対応コミュワード'
    _id = 'OnExecuteSendUserSaidWord'
    SendFeedback(_content,_category,_id)
  } else {
    UserSaid_知らない言葉
  }
}

OnExecuteSendUserSaidWord
{
  UserSaid_知らない言葉
}

OnExecuteSendUserSaidWordFailure
{
  UserSaid_知らない言葉
}

/*-----------------------------------------------------------------------------
  選択コミュニケート台詞リスト
-----------------------------------------------------------------------------*/
CommWordList : nonoverlap
{
	"\q[「朝ごはんは食べたかい？」,UserSaid_朝ごはん食べた？]"
	"\q[「おはよ、姫織くん」,UserSaid_おはよう]"
}

/*-----------------------------------------------------------------------------
  フリーコミュニケート
-----------------------------------------------------------------------------*/
FreeCommunicate
{
  _content = _argv[0]
  if (RE_SEARCH(_content,'おは|はよ')) {
    UserSaid_おはよう
  } else {
    UserSaidIgnorant(_content)
  }
}